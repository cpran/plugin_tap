# This script is part of the testsimple CPrAN plugin for Praat.
# The latest version is available through CPrAN or at
# <http://cpran.net/plugins/testsimple>
#
# The testsimple plugin is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public
# License as published by the Free Software Foundation, either
# version 3 of the License, or (at your option) any later version.
#
# The testsimple plugin is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty
# of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with selection. If not, see <http://www.gnu.org/licenses/>.
#
# Copyright 2015-2016 Jose Joaquin Atria

#!
#! # A test suite for Praat with diagnostics
#!
#! Unlike the procedures in `simple.proc`, these procedure implement
#! testing interface for Praat modeled after the Test::More Perl module.
#! With it, diagnostics for failing tests can be obtained, making error
#! reports a bit more informative.
#!
#! If you already use the procedures in `simple.proc`, you can
#! change into using these procedures without changing your code at all.
#! You won't fully benefit from the procedures in this file, however,
#! until you do make those changes.
#!

if !variableExists("diag.prefix$")
  diag.prefix$ = "#   "
endif

#!
#! # Procedure overrides
#!

#! ~~~ params
#! in:
#!   .value: >
#!     A test value, interpreted as a boolean: `true` means success,
#!     `false` means failure.
#!   .name$: >
#!     The name of the test. A descriptive string with which to identify it.
#! out: Internally calls `@_testsimple_ok`
#! ~~~
#!
#! The basic exposed test procedure. Failing tests will also be marked with
#! an additional diagnostic line.
#!
procedure ok: .value, .name$
  @_testsimple_ok()

  .todo$ = if ok.todo then "(TODO) " else "" fi
  if !ok.value
    @diag: "Failed " + .todo$ + "test '" + .name$ + "'"
  endif
endproc

#! ~~~ params
#! out: Internally calls `@ok`.
#! ~~~
#!
#! Test whether objects created during testing have been removed. The check
#! is made by comparing the number of objects when the plan was specified
#! to the number of objects when the test was called. If they are different,
#! the test will fail.
#!
#! If the test fails, a list of the objects remaining in the list will be
#! printed, with their names and ID numbers.
#!
procedure ok_selection ()
  select all
  minusObject: plan.original
  @_testsimple_saveSelection: "final_selection"
  .current = '_testsimple_saveSelection.table'

  @ok: Object_'plan.original'.nrow == Object_'.current'.nrow,
    ... "Removed all local objects"

  if !ok.value
    if Object_'plan.original'.nrow > Object_'.current'.nrow
      @diag: "  these original objects are missing:"
      for .i to Object_'plan.original'.nrow
        .oid = Object_'plan.original'[.i, "id"]
        .name$ = Object_'plan.original'$[.i, "id"] + ". " +
          ... Object_'plan.original'$[.i, "type"]  + " "  +
          ... Object_'plan.original'$[.i, "name"]

        @_ok_objectExists: .oid
        if !'_ok_objectExists.return'
          @diag: "  - " + .name$
        endif
      endfor
    elsif Object_'plan.original'.nrow < Object_'.current'.nrow
      @diag: "  these objects remain in the list:"
      @_testsimple_restoreSelection: .current
      @_testsimple_minusSelection: plan.original
      for .i to numberOfSelected()
        @diag: "  - " + selected$(.i)
      endfor
    endif
  endif

  removeObject: .current
endproc

include ../../plugin_testsimple/procedures/simple.proc

procedure is$: .got$, .expected$, .msg$
  if .got$ == .expected$
    @ok: 1, .msg$
  else
    @ok: 0, .msg$
    if !ok.value
      @diag: "       got: """      + .got$ + """"
      @diag: "  expected: """ + .expected$ + """"
    endif
  endif
endproc

procedure is: .got, .expected, .msg$
  if .got == .expected
    @ok: 1, .msg$
  else
    @ok: 0, .msg$
    if !ok.value
      @diag: "       got: " + string$(.got)
      @diag: "  expected: " + string$(.expected)
    endif
  endif
endproc

procedure isnt$: .got$, .expected$, .msg$
  if .got$ != .expected$
    @ok: 1, .msg$
  else
    @ok: 0, .msg$
    if !ok.value
      @diag: "       got: """      + .got$ + """"
      @diag: "  expected: anything else"
    endif
  endif
endproc

procedure isnt: .got, .expected, .msg$
  if .got != .expected
    @ok: 1, .msg$
  else
    @ok: 0, .msg$
    if !ok.value
      @diag: "       got: " + string$(.got)
      @diag: "  expected: anything else"
    endif
  endif
endproc

procedure diag: .msg$
  .msg$ = .prefix$ + .msg$
  .msg$ = replace_regex$(.msg$, newline$, newline$ + .prefix$, 0)
  appendInfoLine: .msg$
endproc

procedure like: .got$, .pattern$, .msg$
  if index_regex(.got$, .pattern$)
    @ok: 1, .msg$
  else
    @ok: 0, .msg$
    if !ok.value
      @diag: "                '" + .got$     + "'"
      @diag: "  doesn't match /" + .pattern$ + "/"
    endif
  endif
endproc

procedure unlike: .got$, .pattern$, .msg$
  if !index_regex(.got$, .pattern$)
    @ok: 1, .msg$
  else
    @ok: 0, .msg$
    if !ok.value
      @diag: "                '" + .got$     + "'"
      @diag: "        matches /" + .pattern$ + "/"
    endif
  endif
endproc

procedure cmp_ok$: .got$, .op$, .expected$, .msg$
  if .got$ '.op$' .expected$
    @ok: 1, .msg$
  else
    @ok: 0, .msg$
    if !ok.value
      @diag: "  '" + .got$      + "'"
      @diag: "      " + .op$
      @diag: "  '" + .expected$ + "'"
    endif
  endif
endproc

procedure cmp_ok: .got, .op$, .expected, .msg$
  if .got '.op$' .expected
    @ok: 1, .msg$
  else
    @ok: 0, .msg$
    if !ok.value
      @diag: "  '" + string$(.got)      + "'"
      @diag: "      " + .op$
      @diag: "  '" + string$(.expected) + "'"
    endif
  endif
endproc

procedure ok_exists: .id, .msg$
  @objectExists: .id

  if objectExists.return
    @ok: 1, .msg$
  else
    @ok: 0, .msg$
    if !ok.value
      @diag: "  object '" + string$(.id) + "' doesn't exist"
    endif
  endif
endproc

procedure no_object: .id, .msg$
  @objectExists: .id

  if !objectExists.return
    @ok: 1, .msg$
  else
    @ok: 0, .msg$
    if !ok.value
      @diag: "  object '" + string$(.id) + "' exists"
    endif
  endif
endproc

procedure ok_selected: .id, .msg$
  @objectExists: .id

  if objectExists.return
    @isSelected: .id
    if isSelected.return
      @ok: 1, .msg$
    else
      @ok: 0, .msg$
      if !ok.value
        @diag: "  object '" + string$(.id) + "' is not selected"
      endif
    endif
  else
    @ok_exists: 0, .msg$
  endif
endproc

procedure ok_unselected: .id, .msg$
  @_ok_objectExists: .id

  if '_ok_objectExists.return'
    @isSelected: .id
    if !isSelected.return
      @ok: 1, .msg$
    else
      @ok: 0, .msg$
      if !ok.value
        @diag: "  object '" + string$(.id) + "' is selected"
      endif
    endif
  else
    @ok_exists: 0, .msg$
  endif
endproc

procedure is_deeply: .a, .b, .msg$
  .result = undefined

  .n = numberOfSelected()
  for .i to .n
    .id[.n] = selected(.i)
  endfor

  .adiag$ = string$(.a)
  .bdiag$ = string$(.b)

  @_ok_objectExists: .a
  if !'_ok_objectExists.return'
    .result = 0
    .aexists = 0
    .adiag$ = "'" + .adiag$ + "'"
  else
    selectObject: .a
    .aexists = 1
    .adiag$ = .adiag$ + ". " + selected$()
  endif

  @_ok_objectExists: .b
  if !'_ok_objectExists.return'
    .result = 0
    .bexists = 0
    .bdiag$ = "'" + .bdiag$ + "'"
  else
    selectObject: .b
    .bexists = 1
    .bdiag$ = .bdiag$ + ". " + selected$()
  endif

  if .result == undefined
    if objectsAreIdentical(.a, .b)
      @ok: 1, .msg$
    else
      @ok: 0, .msg$
      if !ok.value
        @diag: "      objects"
        @diag: "  " + .adiag$
        @diag: "      and"
        @diag: "  " + .bdiag$
        @diag: "      are not the same"
      endif
    endif
  else
    @ok: 0, .msg$
    if !ok.value
      if !.aexists and !.bexists
        @diag: "      neither object"
        @diag: "  " + .adiag$
        @diag: "      nor object"
        @diag: "  " + .bdiag$
        @diag: "      exist"
      elsif .aexists
        @diag: "  " + .adiag$
        @diag: "      exists but"
        @diag: "  " + .bdiag$
        @diag: "      doesn't"
      else
        @diag: "  " + .adiag$
        @diag: "      doesn't exist but"
        @diag: "  " + .bdiag$
        @diag: "      does"
      endif
    endif
  endif

  nocheck selectObject: undefined
  for .i to .n
    plusObject: .id[.n]
  endfor
endproc

procedure pass: .msg$
  @ok: 1, .msg$
endproc

procedure fail: .msg$
  @ok: 0, .msg$
endproc

procedure BAIL_OUT: .msg$
endproc

# From "selection" plugin, copied here to limit dependencies
procedure _ok_objectExists (.id)
  .start = numberOfSelected()
  nocheck plusObject: .id
  .plus = numberOfSelected()
  nocheck minusObject: .id
  .minus = numberOfSelected()

  if .plus = .minus
    # Object does not exist
    .return = 0
  else
    .return = 1
    if .start < .plus
      minusObject: .id
    else
      plusObject: .id
    endif
  endif
endproc

# From "selection" plugin, copied here to limit dependencies
procedure _ok_isSelected (.id)
  .n = numberOfSelected()
  .return = 0
  for .i to .n
    if selected(.i) == .id
      .return = 1
      .i += .n
    endif
  endfor
endproc
